@PACKAGE_INIT@

include(CMakeFindDependencyMacro)

find_dependency(Threads)

find_dependency(MsQuic)

include("${CMAKE_CURRENT_LIST_DIR}/BeyondProtocolTargets.cmake")

if (@WITH_OPENSSL@)
	set(OPENSSL_USE_STATIC_LIBS TRUE)
	if(WIN32)
		# On Windows also checks if OpenSSL binaries are available with MsQuic 
		if(NOT DEFINED OPENSSL_ROOT_DIR)
			set(OPENSSL_ROOT_DIR "${MsQuic_DIR}/../..")
			find_package(OpenSSL)
		endif()
	endif()
	if (NOT OPENSSL_FOUND)
		find_package(OpenSSL REQUIRED)
	endif()
	set(HAVE_OPENSSL TRUE)
endif()

check_required_components(BeyondProtocol)

if (NOT EXISTS @PACKAGE_INCLUDE_INSTALL_DIR@ AND EXISTS @PACKAGE_CMAKE_CURRENT_SOURCE_DIR@/include)
	# Workaround when using build directory instead of install directory
	set_and_check(PROTOCOL_INCLUDE_DIRS @PACKAGE_CMAKE_CURRENT_SOURCE_DIR@/include)
else()
	# Default: use install (package) includes if they exist
	set_and_check(PROTOCOL_INCLUDE_DIRS @PACKAGE_INCLUDE_INSTALL_DIR@)
endif()
